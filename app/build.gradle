apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: 'com.google.protobuf'

kapt {
    generateStubs = true
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.5.1-1'
    }
    plugins {
        javalite { artifact = "com.google.protobuf:protoc-gen-javalite:3.0.0" }
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.8.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                javalite {}
                grpc { // Options added to --grpc_out
                    option 'lite'
                }
            }
        }
    }
}

android {
    compileSdkVersion rootProject.compileSdkVersion
    defaultConfig {
        applicationId "com.veronnnetworks.veronnwallet"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    dataBinding {
        enabled = true
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // common
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.appcompat:appcompat:$appcompat_ver"
    implementation "androidx.core:core-ktx:$core_ktx_ver"
    implementation "androidx.constraintlayout:constraintlayout:$constraint_ver"
    implementation "com.google.android.material:material:$material_ver"
    implementation "androidx.legacy:legacy-support-v4:$legacy_support_v4_ver"

    // ViewModel and LiveData
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_ver"
    implementation "androidx.lifecycle:lifecycle-reactivestreams:$lifecycle_ver"

    // rx
    implementation "io.reactivex.rxjava2:rxjava:$rx_java_ver"
    implementation "io.reactivex.rxjava2:rxandroid:$rx_android_ver"
    implementation "io.reactivex.rxjava2:rxkotlin:$rx_kotlin_ver"

    // Dagger2
    implementation "com.google.dagger:dagger:$dagger_ver"
    implementation "com.google.dagger:dagger-android-support:$dagger_ver"
    kapt "com.google.dagger:dagger-compiler:$dagger_ver"
    kapt "com.google.dagger:dagger-android-processor:$dagger_ver"

    // retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_ver"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_ver"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_ver"
    implementation "com.squareup.moshi:moshi-kotlin:$moshi_ver"

    // log
    implementation "com.jakewharton.timber:timber:$timber_ver"

    // crypto
    implementation "com.madgag.spongycastle:prov:$spongy_ver"
    implementation "com.madgag.spongycastle:bcpkix-jdk15on:$spongy_ver"

    // grpc
    implementation "io.grpc:grpc-okhttp:$grpc_ver"
    implementation "io.grpc:grpc-protobuf-lite:$grpc_ver"
    implementation "io.grpc:grpc-stub:$grpc_ver"
    compileOnly "javax.annotation:javax.annotation-api:1.3.2"

    implementation "com.google.code.gson:gson:$gson_ver"

    testImplementation "org.robolectric:robolectric:$roboletric_ver"
    testImplementation "junit:junit:$junit_ver"

    androidTestImplementation "androidx.test:runner:$runner_ver"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_ver"
}
